plugins {
	id("mod-platform")
	id("net.fabricmc.fabric-loom")
}

platform {
	loader = "fabric"
	dependencies {
		required("minecraft") {
			versionRange = ">=${prop("deps.minecraft").replace("rc-", "rc.")}"
		}
		required("fabric-api") {
			slug("fabric-api")
			versionRange = ">=${prop("deps.fabric-api")}"
		}
		required("fabricloader") {
			versionRange = ">=${libs.fabric.loader.get().version}"
		}
		required("fzzy_config") {
			slug("fzzy-config")
			versionRange = "*"
		}
		required("mixson") {
			slug("mixson")
			versionRange = "*"
		}
		required("defaulted") {
			slug("defaulted")
			versionRange = ">=1.3.1"
		}
		optional("modmenu") {}
	}
}

loom {
	accessWidenerPath = rootProject.file("src/main/resources/aw/${stonecutter.current.version}.accesswidener")
	runs.named("client") {
		client()
		ideConfigGenerated(true)
		runDir = "run/"
		environment = "client"
		programArgs("--username=Dev")
		configName = "Fabric Client"
	}
	runs.named("server") {
		server()
		ideConfigGenerated(true)
		runDir = "run/"
		environment = "server"
		configName = "Fabric Server"
	}
}

repositories {
	mavenCentral()
	strictMaven("https://maven.fzzyhmstrs.me/", "me.fzzyhmstrs") { name = "Fzzy Config" }
	strictMaven("https://maven.terraformersmc.com/", "com.terraformersmc") { name = "TerraformersMC" }
	strictMaven("https://jitpack.io") { name = "Jitpack" }
	strictMaven("https://api.modrinth.com/maven", "maven.modrinth") { name = "Modrinth" }
}

dependencies {
	minecraft("com.mojang:minecraft:${prop("deps.minecraft")}")
	implementation(libs.fabric.loader)
	implementation(libs.moulberry.mixinconstraints)
	include(libs.moulberry.mixinconstraints)
	implementation("net.fabricmc.fabric-api:fabric-api:${prop("deps.fabric-api")}")
	localRuntime("com.terraformersmc:modmenu:${prop("deps.modmenu")}")
	implementation("me.fzzyhmstrs:fzzy_config:${prop("deps.fzzy_config")}")
	implementation("maven.modrinth:mixson:${prop("deps.mixson")}") {
		exclude(group = "net.fabricmc.fabric-api", module = "fabric-api")
	}
	implementation("maven.modrinth:defaulted:${prop("deps.defaulted")}")

	compileOnly("maven.modrinth:penchant:${prop("deps.penchant")}")
	runtimeOnly("maven.modrinth:penchant:${prop("deps.penchant")}")
	compileOnly("maven.modrinth:enchantment-disabler:${prop("deps.ed")}-fabric")
	compileOnly("maven.modrinth:tax-free-levels:${prop("deps.tfl")}-fabric")
}
